;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; TWAIN Direct Scan v1.0 22-Jun-2017
; arg:1 - scanner to use
; arg:2 - task (optional)
; returns: pass or fail
;
; Scan using TWAIN Direct...
;



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Pick a scanner, default to the first scanner if we
; don't get one...
;
select '${arg:1}'
if '${ret:}' != 'true' goto MAIN.FAIL



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Get the scanner info, including its x-privet-token...
;
echo 'CMD: infoex'
infoex
if '${rj:x-privet-token}' == '' goto MAIN.FAIL
echo 'SCANNER:' '${rj:description}' / '${rj:name}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Create a new session...
;
echo 'CMD: createSession'
createSession
if '${rj:results.success}' != 'true' goto MAIN.FAIL
if '${rj:results.session.state}' != 'ready' goto MAIN.FAIL



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Wait for events (we won't get a response)...
;
echo 'CMD: waitForEvents'
waitForEvents



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Send a task...
;
set task '${arg:2}'
if '${get:task}' != '' goto MAIN.SENDTASK
set task '{"actions":[{"action":"configure","streams":[{"sources":[{"source":"any","pixelFormats":[{"attributes":[{"attribute":"numberOfSheets","values":[{"value":1}]}]}]}]}]}]}'
:MAIN.SENDTASK
echo 'CMD: sendTask'
sendTask '${get:task}'
if '${rj:results.success}' != 'true' goto MAIN.FAIL



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Start capturing images...
;
echo 'CMD: startCapturing'
startCapturing
if '${rj:results.success}' != 'true' goto ERROR
if '${rj:results.session.state}' != 'capturing' goto ERROR



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Scan until the scanner stops giving us images...
;
; Loop here until we see an image, or scanning ends...
:MAIN.WAIT

	; We should only wait if more images could be coming...
	if '${session:imageBlocksDrained}' == 'false' goto MAIN.WAITFORSESSIONUPDATE
	goto MAIN.DONE

	; Wait for the session object to be updated...
	:MAIN.WAITFORSESSIONUPDATE
	echo
	echo 'WaitForSessionUpdate 5000'
	waitforsessionupdate 5000
	echo 'signaled: ${ret:}'

	; If we have an image, get it...
	set firstimageblock '${session:imageBlocks[0]}'
	if '${get:firstimageblock}' != '' goto MAIN.GETIMAGE
	goto MAIN.WAIT

goto MAIN.WAITNEXT

; Keep getting images as long as we have some...
:MAIN.GETIMAGE

	; Get the metadata for the first image block, no thumbnail...
	echo
	echo 'CMD: readImageBlockMetadata' '${get:firstimageblock}' 'false'
	readImageBlockMetadata '${get:firstimageblock}' 'false'
	if '${rj:results.success}' != 'true' goto MAIN.FAIL

	; Get the first image block, no metadata...
	echo 'CMD: readImageBlock' '${get:firstimageblock}' 'false
	readImageBlock '${get:firstimageblock}' 'false'
	if '${rj:results.success}' != 'true' goto MAIN.FAIL

	; release the first image block...
	echo 'CMD: releaseImageBlocks' '${get:firstimageblock}' '${get:firstimageblock}'
	releaseImageBlocks '${get:firstimageblock}' '${get:firstimageblock}'
	set firstimageblock '${rj:results.session.imageBlocks[0]}'
	if '${rj:results.success}' != 'true' goto MAIN.FAIL
	if '${rj:results.session.imageBlocksDrained}' != 'false' goto MAIN.DONE
	if '${get:firstimageblock}' == '' goto MAIN.WAIT

goto MAIN.GETIMAGE
:MAIN.DONE



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Stop capturing images...
;
echo
echo 'CMD: stopCapturing'
stopCapturing



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Close the session...
;
echo 'CMD: closeSession'
closeSession



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Bye-bye...
;
echo 'Done...'
return pass



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; MAIN.FAIL
;
:MAIN.FAIL
stopCapturing
closeSession
echo 'script failed...'
return 'fail'
